plugins {
    id "de.undercouch.download" version "4.1.1"
}

apply plugin: 'com.android.application'
apply plugin: 'de.undercouch.download'

android {
    compileSdkVersion 29
    buildToolsVersion '29.0.2'

    defaultConfig {
        applicationId "info.melda.sala.zetemezszam"
        minSdkVersion 14
        targetSdkVersion 29
        project.ext.set("archivesBaseName", "info.melda.sala.ZeteMezszam" )
    }

    signingConfigs {
        release {
            def RELEASE_STORE_FILE=project.properties['RELEASE_STORE_FILE'] ?: "release.keystore"
            storeFile file(RELEASE_STORE_FILE)
            def RELEASE_STORE_PASSWORD=project.properties['RELEASE_STORE_PASSWORD'] ?: ""
            storePassword RELEASE_STORE_PASSWORD
            keyAlias 'melda'
            def RELEASE_KEY_PASSWORD=project.properties['RELEASE_KEY_PASSWORD'] ?: ""
            keyPassword RELEASE_KEY_PASSWORD

            v1SigningEnabled true
            v2SigningEnabled true
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            signingConfig signingConfigs.release
        }
    }
}

dependencies {
    api 'androidx.appcompat:appcompat:1.2.0'
}

import de.undercouch.gradle.tasks.download.Download

task updatePlayerInfo(type: Download) {
    src ([
            'https://sala.melda.info/mezszam/seasons.csv',
            'https://sala.melda.info/mezszam/players.csv',
            'https://sala.melda.info/mezszam/conf.csv',
            'https://sala.melda.info/mezszam/shirts.csv'
    ])
    dest 'src/main/res/raw/'
    onlyIfNewer true
    quiet false
}
