plugins {
    id "de.undercouch.download" version "5.1.0"
}

apply plugin: 'com.android.application'
apply plugin: 'de.undercouch.download'

project.ext.versionName = "1.00.10"
project.ext.packageName = "info.melda.sala.zetemezszam"
project.archivesBaseName = "${packageName}.ZeteMezszam-${versionName}"

android {
    namespace "info.melda.sala.zetemezszam"
    compileSdkVersion 34
//    buildToolsVersion '34.0.0'

    defaultConfig {
        applicationId "${packageName}"
        minSdkVersion 14
        targetSdkVersion 34
    }

    signingConfigs {
        release {
            def RELEASE_STORE_FILE=project.properties['RELEASE_STORE_FILE'] ?: "release.keystore"
            storeFile file(RELEASE_STORE_FILE)
            def RELEASE_STORE_PASSWORD=project.properties['RELEASE_STORE_PASSWORD'] ?: ""
            storePassword RELEASE_STORE_PASSWORD
            keyAlias 'melda'
            def RELEASE_KEY_PASSWORD=project.properties['RELEASE_KEY_PASSWORD'] ?: ""
            keyPassword RELEASE_KEY_PASSWORD

            v1SigningEnabled true
            v2SigningEnabled true
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            signingConfig signingConfigs.release
        }
    }
}

dependencies {
    api 'androidx.appcompat:appcompat:1.6.1'
}

task updatePlayerInfo(type: Download) {
    group = "Update"
    src ([
            'https://sala.melda.info/mezszam/seasons.csv',
            'https://sala.melda.info/mezszam/players.csv',
            'https://sala.melda.info/mezszam/conf.csv',
            'https://sala.melda.info/mezszam/shirts.csv'
    ])
    dest 'src/main/res/raw/'
    onlyIfNewer true
    quiet false
}
